apply plugin: 'groovy'
apply plugin: 'idea'

group = 'net.gvmtool'
archivesBaseName = 'gradle-sdkvendor-plugin'
version = '0.3.0'

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()

    compile 'com.github.groovy-wslite:groovy-wslite:0.8.0'
    compile 'org.hibernate:hibernate-validator:5.1.3.Final'
    compile 'javax.el:javax.el-api:2.2.4'
    compile 'org.glassfish.web:javax.el:2.2.4'
    testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
    testCompile 'junit:junit:4.11'
    testCompile 'com.github.tomakehurst:wiremock:1.46'
}

apply plugin: 'distribution'

distributions {
    main {
        baseName = archivesBaseName
        contents {
            from { libsDir }
        }
    }
}

ext {
    bintrayBaseUrl = 'https://api.bintray.com/maven'
    bintrayUsername = 'vermeulen-mp'
    bintrayRepository = 'gradle-plugins'
    bintrayPackage = 'gradle-sdkvendor-plugin'
}

apply plugin: 'maven-publish'

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'source'
}

publishing {
    publications {
        plugin(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier "sources"
            }

            pom.withXml {
                def root = asNode()
                root.appendNode('name', 'Gradle GVM Vendor plugin')
                root.appendNode('description', 'Gradle plugin for GVM Vendors to publish Candidate Versions.')
                root.appendNode('inceptionYear', '2014')

                def license = root.appendNode('licenses').appendNode('license')
                license.appendNode('name', 'The Apache Software License, Version 2.0')
                license.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
                license.appendNode('distribution', 'repo')

                def developer = root.appendNode('developers').appendNode('developer')
                developer.appendNode('id', 'vermeulen.mp')
                developer.appendNode('name', 'Marco Vermeulen')
                developer.appendNode('email', 'vermeulen.mp@gmail.com')
            }

        }
    }

    repositories {
        maven {
            name 'Bintray'
            url "$bintrayBaseUrl/$bintrayUsername/$bintrayRepository/$bintrayPackage"

            credentials {
                username = System.getenv('BINTRAY_USERNAME') ?: 'invalid_user'
                password = System.getenv('BINTRAY_API_KEY') ?: 'invalid_key'
            }
        }
    }
}
